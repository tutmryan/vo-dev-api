@description('The resource prefix to use for all resources')
@minLength(3)
param resourcePrefix string

param location string = resourceGroup().location

resource logAnalytics 'Microsoft.OperationalInsights/workspaces@2022-10-01' = {
  name: '${resourcePrefix}-la'
  location: location
  properties: {
    sku: {
      name: 'PerGB2018'
    }
    retentionInDays: 180
    features: {
      enableDataExport: true
    }
  }
}

resource sqlServerUserAssignedIdentity 'Microsoft.ManagedIdentity/userAssignedIdentities@2023-01-31' = {
  name: '${resourcePrefix}-sql-server-identity'
  location: location
}

@description('Name of the Azure SQL AAD administrator')
param sqlServerAadAdministratorName string

@description('Object ID of the Azure SQL AAD administrator')
param sqlServerAadAdministratorObjectId string

resource sqlServer1 'Microsoft.Sql/servers@2022-05-01-preview' = {
  name: '${resourcePrefix}-sql-server-1'
  location: location
  identity: {
    userAssignedIdentities: {
      '${sqlServerUserAssignedIdentity.id}': {}
    }
    type: 'UserAssigned'
  }
  properties: {
    publicNetworkAccess: 'Disabled'
    primaryUserAssignedIdentityId: sqlServerUserAssignedIdentity.id
    administrators: {
      azureADOnlyAuthentication: true
      administratorType: 'ActiveDirectory'
      login: sqlServerAadAdministratorName
      sid: sqlServerAadAdministratorObjectId
      tenantId: subscription().tenantId
    }
  }
}

resource sqlServer1AuditingSettings 'Microsoft.Sql/servers/auditingSettings@2023-05-01-preview' = {
  name: 'default'
  parent: sqlServer1
  properties: {
    auditActionsAndGroups: [
      'BATCH_COMPLETED_GROUP'
      'SUCCESSFUL_DATABASE_AUTHENTICATION_GROUP'
      'FAILED_DATABASE_AUTHENTICATION_GROUP'
    ]
    isAzureMonitorTargetEnabled: true
    isDevopsAuditEnabled: true
    state: 'Enabled'
  }
}

resource sqlServer1Master 'Microsoft.Sql/servers/databases@2020-08-01-preview' existing = {
  name: 'master'
  parent: sqlServer1
}

resource sqlServer1MasterDiagnostics 'Microsoft.Insights/diagnosticSettings@2021-05-01-preview' = {
  scope: sqlServer1Master
  name: 'diagnostics'
  properties: {
    workspaceId: logAnalytics.id
    logs: [
      {
        category: 'SQLSecurityAuditEvents'
        enabled: true
      }
      {
        category: 'DevOpsOperationsAudit'
        enabled: true
      }
    ]
  }
}

@description('Elastic Pool edition')
@allowed(['Basic', 'Standard', 'Premium', 'GP_Gen5', 'BC_Gen5'])
param elasticPoolEdition string = 'Basic'

@description('Elastic Pool Capacity (vCores or DTU, depends on tier, please refer to docs)')
param elasticPoolCapacity int

@description('Elastic Pool zone redundant')
param elasticPoolZoneRedundant bool = false

@description('Elastic Pool - The minimum capacity any one database can consume, .')
param elasticPoolPerDatabaseMinCapacity int = 0

@description('Elastic Pool - The maximum capacity any one database can consume, not set if left as default.')
param elasticPoolPerDatabaseMaxCapacity int = 0

var editionToSkuMap = {
  Basic: {
    name: 'BasicPool'
    tier: 'Basic'
  }
  Standard: {
    name: 'StandardPool'
    tier: 'Standard'
  }
  Premium: {
    name: 'PremiumPool'
    tier: 'Premium'
  }
  GP_Gen5: {
    family: 'Gen5'
    name: 'GP_Gen5'
    tier: 'GeneralPurpose'
  }
  BC_Gen5: {
    family: 'Gen5'
    name: 'BC_Gen5'
    tier: 'BusinessCritical'
  }
}

resource sqlServerElasticPool 'Microsoft.Sql/servers/elasticPools@2022-05-01-preview' = {
  name: '${resourcePrefix}-sql-elastic-pool'
  location: location
  sku: {
    capacity: elasticPoolCapacity
    name: editionToSkuMap[elasticPoolEdition].name
    tier: editionToSkuMap[elasticPoolEdition].tier
  }
  parent: sqlServer1
  properties: {
    zoneRedundant: elasticPoolZoneRedundant
    perDatabaseSettings: union(
      { minCapacity: elasticPoolPerDatabaseMinCapacity },
      elasticPoolPerDatabaseMaxCapacity > 0 ? { maxCapacity: elasticPoolPerDatabaseMaxCapacity } : {}
    )
  }
}

resource sqlServerPrivateEndpoint 'Microsoft.Network/privateEndpoints@2023-05-01' = {
  name: '${resourcePrefix}-sql-pe'
  location: location
  properties: {
    subnet: {
      id: '${vnet.id}/subnets/private-endpoints-subnet'
    }
    privateLinkServiceConnections: [
      {
        name: '${resourcePrefix}-sql-server-1-private-link'
        properties: {
          privateLinkServiceId: sqlServer1.id
          groupIds: [
            'sqlServer'
          ]
        }
      }
    ]
  }
}

resource sqlServerPrivateDnsZoneGroup 'Microsoft.Network/privateEndpoints/privateDnsZoneGroups@2023-05-01' = {
  parent: sqlServerPrivateEndpoint
  name: 'default'
  properties: {
    privateDnsZoneConfigs: [
      {
        name: 'config1'
        properties: {
          privateDnsZoneId: sqlServerPrivateDnsZone.id
        }
      }
    ]
  }
}

output sqlServer1Name string = sqlServer1.name

@description('App Service Plan SKU')
@allowed([
  'B1'
  'B2'
  'B3'
  'D1'
  'F1'
  'FREE'
  'I1'
  'I1v2'
  'I2'
  'I2v2'
  'I3'
  'I3v2'
  'I4v2'
  'I5v2'
  'I6v2'
  'P0V3'
  'P1MV3'
  'P1V2'
  'P1V3'
  'P2MV3'
  'P2V2'
  'P2V3'
  'P3MV3'
  'P3V2'
  'P3V3'
  'P4MV3'
  'P5MV3'
  'S1'
  'S2'
  'S3'
  'SHARED'
  'WS1'
  'WS2'
  'WS3'
])
param appServicePlanSku string

@description('App Service Plan Capacity (instances)')
param appServicePlanCapacity int = 1

@description('App Service Plan zone redundancy')
param appServiceZoneRedundant bool = false

@description('Enable autoscale for the App Service Plan')
param appServiceAutoscale bool = true

resource appServicePlan1 'Microsoft.Web/serverfarms@2022-03-01' = {
  name: '${resourcePrefix}-app-service-plan-1'
  location: location
  sku: {
    name: appServicePlanSku
    capacity: appServicePlanCapacity
  }
  properties: {
    reserved: true
    zoneRedundant: appServiceZoneRedundant
    elasticScaleEnabled: appServiceAutoscale
    maximumElasticWorkerCount: appServiceAutoscale ? 30 : 0
  }
  kind: 'linux'
}

output appServicePlan1Id string = appServicePlan1.id

@description('The name of the shared action group for alerts')
param actionGroupAlertName string

@description('The display or short name for the shared action group used for alerts (e.g., SMS notifications). Maximum length: 12 characters.')
param actionGroupAlertShortName string

@description('The email the actionGroupAlert will send alerts to')
param actionGroupAlertEmail string

resource actionGroupAlert 'Microsoft.Insights/actionGroups@2023-09-01-preview' = {
  name: actionGroupAlertName
  location: 'Global'
  properties: {
    groupShortName: actionGroupAlertShortName
    enabled: true
    emailReceivers: [
      {
        name: '${actionGroupAlertName}-EmailAction'
        emailAddress: actionGroupAlertEmail
        useCommonAlertSchema: false
      }
    ]
  }
}

@description('The name of the SQL Server Elastic Pool alert')
param sqlServerElasticPoolAlertName string

resource sqlServerElasticPoolAlert 'Microsoft.Insights/metricAlerts@2018-03-01' = {
  name: sqlServerElasticPoolAlertName
  location: 'global'
  properties: {
    description: 'Triggers alerts when critical thresholds are reached in the SQL Elastic Pool resource.'
    severity: 0
    enabled: true
    scopes: [
      sqlServerElasticPool.id
    ]
    evaluationFrequency: 'PT1M'
    windowSize: 'PT5M'
    criteria: {
      allOf: [
        {
          threshold: 70
          name: 'Storage Percent'
          metricNamespace: 'Microsoft.Sql/servers/elasticpools'
          metricName: 'storage_percent'
          operator: 'GreaterThan'
          timeAggregation: 'Average'
          skipMetricValidation: false
          criterionType: 'StaticThresholdCriterion'
        }
        {
          threshold: 70
          name: 'CPU Percent'
          metricNamespace: 'Microsoft.Sql/servers/elasticpools'
          metricName: 'cpu_percent'
          operator: 'GreaterThan'
          timeAggregation: 'Average'
          skipMetricValidation: false
          criterionType: 'StaticThresholdCriterion'
        }
        {
          threshold: 70
          name: 'DTU Consumption Percent'
          metricNamespace: 'Microsoft.Sql/servers/elasticpools'
          metricName: 'dtu_consumption_percent'
          operator: 'GreaterThan'
          timeAggregation: 'Average'
          skipMetricValidation: false
          criterionType: 'StaticThresholdCriterion'
        }
      ]
      'odata.type': 'Microsoft.Azure.Monitor.MultipleResourceMultipleMetricCriteria'
    }
    autoMitigate: true
    targetResourceType: 'Microsoft.Sql/servers/elasticpools'
    targetResourceRegion: location
    actions: [
      {
        actionGroupId: actionGroupAlert.id
      }
    ]
  }
}

@description('The name of the app service plan alert')
param appServicePlanAlertName string

resource appServicePlanAlert 'Microsoft.Insights/metricAlerts@2018-03-01' = {
  name: appServicePlanAlertName
  location: 'global'
  properties: {
    description: 'Triggers alerts when critical thresholds are reached in the App Service Plan resource.'
    severity: 0
    enabled: true
    scopes: [
      appServicePlan1.id
    ]
    evaluationFrequency: 'PT1M'
    windowSize: 'PT5M'
    criteria: {
      allOf: [
        {
          threshold: 80
          name: 'CPU Percentage'
          metricNamespace: 'Microsoft.Web/serverFarms'
          metricName: 'CpuPercentage'
          operator: 'GreaterThan'
          timeAggregation: 'Average'
          skipMetricValidation: false
          criterionType: 'StaticThresholdCriterion'
        }
        {
          threshold: 80
          name: 'Memory Percentage'
          metricNamespace: 'Microsoft.Web/serverFarms'
          metricName: 'MemoryPercentage'
          operator: 'GreaterThan'
          timeAggregation: 'Average'
          skipMetricValidation: false
          criterionType: 'StaticThresholdCriterion'
        }
        {
          threshold: 100
          name: 'Http Queue Length'
          metricNamespace: 'Microsoft.Web/serverFarms'
          metricName: 'HttpQueueLength'
          operator: 'GreaterThan'
          timeAggregation: 'Average'
          skipMetricValidation: false
          criterionType: 'StaticThresholdCriterion'
        }
      ]
      'odata.type': 'Microsoft.Azure.Monitor.MultipleResourceMultipleMetricCriteria'
    }
    autoMitigate: true
    targetResourceType: 'Microsoft.Web/serverFarms'
    targetResourceRegion: location
    actions: [
      {
        actionGroupId: actionGroupAlert.id
      }
    ]
  }
}

@description('GitHub Enterprise Database ID')
param gitHubEnterpriseDatabaseId string

resource gitHubNetworkSettings 'GitHub.Network/networkSettings@2024-04-02' = {
  location: location
  name: '${resourcePrefix}-github-network-settings'
  properties: {
    businessId: gitHubEnterpriseDatabaseId
    subnetId: '${vnet.id}/subnets/github-actions-subnet'
  }
}

resource vnet 'Microsoft.Network/virtualNetworks@2023-05-01' = {
  name: '${resourcePrefix}-vnet'
  location: location
  properties: {
    addressSpace: {
      addressPrefixes: [
        '10.0.0.0/16'
      ]
    }
    subnets: [
      {
        name: 'app-service-subnet'
        properties: {
          addressPrefix: '10.0.1.0/24'
          delegations: [
            {
              name: 'Microsoft.Web/serverFarms'
              properties: {
                serviceName: 'Microsoft.Web/serverFarms'
              }
            }
          ]
        }
      }
      {
        name: 'private-endpoints-subnet'
        properties: {
          addressPrefix: '10.0.2.0/24'
          privateEndpointNetworkPolicies: 'Disabled'
        }
      }
      {
        name: 'github-actions-subnet'
        properties: {
          addressPrefix: '10.0.3.0/24'
          delegations: [
            {
              name: 'GitHub.Network/networkSettings'
              properties: {
                serviceName: 'GitHub.Network/networkSettings'
              }
            }
          ]
          networkSecurityGroup: {
            id: gitHubActionsNetworkSecurityGroup.id
          }
        }
      }
    ]
  }
}

resource gitHubActionsNetworkSecurityGroup 'Microsoft.Network/networkSecurityGroups@2023-05-01' = {
  name: '${resourcePrefix}-github-actions-nsg'
  location: location
  properties: {
    securityRules: [
      // https://docs.github.com/en/enterprise-cloud@latest/organizations/managing-organization-settings/configuring-private-networking-for-github-hosted-runners-in-your-organization
      {
        name: 'AllowVnetOutBoundOverwrite'
        properties: {
          protocol: 'TCP'
          sourcePortRange: '*'
          destinationPortRange: '443'
          sourceAddressPrefix: '*'
          destinationAddressPrefix: 'VirtualNetwork'
          access: 'Allow'
          priority: 200
          direction: 'Outbound'
          destinationAddressPrefixes: []
        }
      }
      {
        name: 'AllowOutBoundActions'
        properties: {
          protocol: '*'
          sourcePortRange: '*'
          destinationPortRange: '443'
          sourceAddressPrefix: '*'
          access: 'Allow'
          priority: 210
          direction: 'Outbound'
          destinationAddressPrefixes: [
            '4.175.114.51/32'
            '20.102.35.120/32'
            '4.175.114.43/32'
            '20.72.125.48/32'
            '20.19.5.100/32'
            '20.7.92.46/32'
            '20.232.252.48/32'
            '52.186.44.51/32'
            '20.22.98.201/32'
            '20.246.184.240/32'
            '20.96.133.71/32'
            '20.253.2.203/32'
            '20.102.39.220/32'
            '20.81.127.181/32'
            '52.148.30.208/32'
            '20.14.42.190/32'
            '20.85.159.192/32'
            '52.224.205.173/32'
            '20.118.176.156/32'
            '20.236.207.188/32'
            '20.242.161.191/32'
            '20.166.216.139/32'
            '20.253.126.26/32'
            '52.152.245.137/32'
            '40.118.236.116/32'
            '20.185.75.138/32'
            '20.96.226.211/32'
            '52.167.78.33/32'
            '20.105.13.142/32'
            '20.253.95.3/32'
            '20.221.96.90/32'
            '51.138.235.85/32'
            '52.186.47.208/32'
            '20.7.220.66/32'
            '20.75.4.210/32'
            '20.120.75.171/32'
            '20.98.183.48/32'
            '20.84.200.15/32'
            '20.14.235.135/32'
            '20.10.226.54/32'
            '20.22.166.15/32'
            '20.65.21.88/32'
            '20.102.36.236/32'
            '20.124.56.57/32'
            '20.94.100.174/32'
            '20.102.166.33/32'
            '20.31.193.160/32'
            '20.232.77.7/32'
            '20.102.38.122/32'
            '20.102.39.57/32'
            '20.85.108.33/32'
            '40.88.240.168/32'
            '20.69.187.19/32'
            '20.246.192.124/32'
            '20.4.161.108/32'
            '20.22.22.84/32'
            '20.1.250.47/32'
            '20.237.33.78/32'
            '20.242.179.206/32'
            '40.88.239.133/32'
            '20.121.247.125/32'
            '20.106.107.180/32'
            '20.22.118.40/32'
            '20.15.240.48/32'
            '20.84.218.150/32'
          ]
        }
      }
      {
        name: 'AllowOutBoundGitHub'
        properties: {
          protocol: '*'
          sourcePortRange: '*'
          destinationPortRange: '443'
          sourceAddressPrefix: '*'
          access: 'Allow'
          priority: 220
          direction: 'Outbound'
          destinationAddressPrefixes: [
            '140.82.112.0/20'
            '143.55.64.0/20'
            '185.199.108.0/22'
            '192.30.252.0/22'
            '20.175.192.146/32'
            '20.175.192.147/32'
            '20.175.192.149/32'
            '20.175.192.150/32'
            '20.199.39.227/32'
            '20.199.39.228/32'
            '20.199.39.231/32'
            '20.199.39.232/32'
            '20.200.245.241/32'
            '20.200.245.245/32'
            '20.200.245.246/32'
            '20.200.245.247/32'
            '20.200.245.248/32'
            '20.201.28.144/32'
            '20.201.28.148/32'
            '20.201.28.149/32'
            '20.201.28.151/32'
            '20.201.28.152/32'
            '20.205.243.160/32'
            '20.205.243.164/32'
            '20.205.243.165/32'
            '20.205.243.166/32'
            '20.205.243.168/32'
            '20.207.73.82/32'
            '20.207.73.83/32'
            '20.207.73.85/32'
            '20.207.73.86/32'
            '20.207.73.88/32'
            '20.217.135.1/32'
            '20.233.83.145/32'
            '20.233.83.146/32'
            '20.233.83.147/32'
            '20.233.83.149/32'
            '20.233.83.150/32'
            '20.248.137.48/32'
            '20.248.137.49/32'
            '20.248.137.50/32'
            '20.248.137.52/32'
            '20.248.137.55/32'
            '20.26.156.215/32'
            '20.26.156.216/32'
            '20.26.156.211/32'
            '20.27.177.113/32'
            '20.27.177.114/32'
            '20.27.177.116/32'
            '20.27.177.117/32'
            '20.27.177.118/32'
            '20.29.134.17/32'
            '20.29.134.18/32'
            '20.29.134.19/32'
            '20.29.134.23/32'
            '20.29.134.24/32'
            '20.87.245.0/32'
            '20.87.245.1/32'
            '20.87.245.4/32'
            '20.87.245.6/32'
            '20.87.245.7/32'
            '4.208.26.196/32'
            '4.208.26.197/32'
            '4.208.26.198/32'
            '4.208.26.199/32'
            '4.208.26.200/32'
            '4.225.11.196/32'
            '4.237.22.32/32'
          ]
        }
      }
      {
        name: 'AllowStorageOutbound'
        properties: {
          protocol: '*'
          sourcePortRange: '*'
          destinationPortRange: '443'
          sourceAddressPrefix: '*'
          destinationAddressPrefix: 'Storage'
          access: 'Allow'
          priority: 230
          direction: 'Outbound'
          destinationAddressPrefixes: []
        }
      }
    ]
  }
}

resource sqlServerPrivateDnsZone 'Microsoft.Network/privateDnsZones@2020-06-01' = {
  name: 'privatelink${environment().suffixes.sqlServerHostname}'
  location: 'global'
}

resource sqlServerPrivateDnsZoneLink 'Microsoft.Network/privateDnsZones/virtualNetworkLinks@2020-06-01' = {
  parent: sqlServerPrivateDnsZone
  name: '${resourcePrefix}-sql-dns-link'
  location: 'global'
  properties: {
    registrationEnabled: false
    virtualNetwork: {
      id: vnet.id
    }
  }
}

resource keyVaultPrivateDnsZone 'Microsoft.Network/privateDnsZones@2020-06-01' = {
  name: 'privatelink.vaultcore.azure.net'
  location: 'global'
}

resource keyVaultPrivateDnsZoneLink 'Microsoft.Network/privateDnsZones/virtualNetworkLinks@2020-06-01' = {
  parent: keyVaultPrivateDnsZone
  name: '${resourcePrefix}-kv-dns-link'
  location: 'global'
  properties: {
    registrationEnabled: false
    virtualNetwork: {
      id: vnet.id
    }
  }
}

resource redisCachePrivateDnsZone 'Microsoft.Network/privateDnsZones@2020-06-01' = {
  name: 'privatelink.redis.cache.windows.net'
  location: 'global'
}

resource redisCachePrivateDnsZoneLink 'Microsoft.Network/privateDnsZones/virtualNetworkLinks@2020-06-01' = {
  parent: redisCachePrivateDnsZone
  name: '${resourcePrefix}-redis-dns-link'
  location: 'global'
  properties: {
    registrationEnabled: false
    virtualNetwork: {
      id: vnet.id
    }
  }
}

resource storagePrivateDnsZone 'Microsoft.Network/privateDnsZones@2020-06-01' = {
  name: 'privatelink.blob.${environment().suffixes.storage}'
  location: 'global'
}

resource storagePrivateDnsZoneLink 'Microsoft.Network/privateDnsZones/virtualNetworkLinks@2020-06-01' = {
  parent: storagePrivateDnsZone
  name: '${resourcePrefix}-storage-dns-link'
  location: 'global'
  properties: {
    registrationEnabled: false
    virtualNetwork: {
      id: vnet.id
    }
  }
}
